# $Id$
# Maintainer: Dan McGee <dan@archlinux.org>

pkgbase=postgresql93
pkgname=('postgresql93-libs' 'postgresql93-docs' 'postgresql93')
pkgver=9.3.5
_majorver=${pkgver%.*}
pkgrel=1
arch=('i686' 'x86_64')
url="http://www.postgresql.org/"
license=('custom:PostgreSQL')
makedepends=('krb5' 'libxml2' 'python2' 'perl' 'tcl>=8.6.0' 'openssl>=1.0.0')
source=(http://ftp.postgresql.org/pub/source/v${pkgver}/postgresql-${pkgver}.tar.bz2
        postgresql-run-socket.patch
        postgresql.pam postgresql.logrotate
        postgresql93.service postgresql93.tmpfiles.conf postgresql-check-db-dir)
md5sums=('5059857c7d7e6ad83b6d55893a121b59'
         '75c579eed03ffb2312631f0b649175b4'
         '96f82c38f3f540b53f3e5144900acf17'
         'd28e443f9f65a5712c52018b84e27137'
         '1f3da7c718fa2086bd2629eeb873e82e'
         '1c5a1f99e8e93776c593c468e2612985'
         'd1cc59baa2d169862e68a07a26f60833')
sha256sums=('14176ffb1f90a189e7626214365be08ea2bfc26f26994bafb4235be314b9b4b0'
            '8538619cb8bea51078b605ad64fe22abd6050373c7ae3ad6595178da52f6a7d9'
            '57dfd072fd7ef0018c6b0a798367aac1abb5979060ff3f9df22d1048bb71c0d5'
            '6abb842764bbed74ea4a269d24f1e73d1c0b1d8ecd6e2e6fb5fb10590298605e'
            'f468655737318c434b7decc542fce9abfb3a9e2e22af178ab3a5c99d54c1657f'
            '7e086d70e0dcaa6ce45693b4e826ce92d770192b3aff5642319b1ef279d88dc4'
            '28c5b3820e1e4cdd643b19f3f61b4a1b044388570e3421a205404c789af733df')

# install folder
_pkginsmap="postgresql-${_majorver}"

build() {
  cd "${srcdir}/postgresql-${pkgver}"
 
  patch -Np1 < ../postgresql-run-socket.patch

  ./configure --prefix=/usr/local/${_pkginsmap} \
  --mandir=/usr/local/${_pkginsmap}/share/man \
  --datadir=/usr/local/${_pkginsmap}/share/postgresql \
  --docdir=/usr/local/${_pkginsmap}/share/doc/postgresql \
  --sysconfdir=/etc \
  --with-krb5 \
  --with-gssapi \
  --with-libxml \
  --with-openssl \
  --with-perl \
  --with-python PYTHON=/usr/bin/python2 \
  --with-tcl \
  --with-pam \
  --with-system-tzdata=/usr/share/zoneinfo \
  --enable-nls \
  --enable-thread-safety

  make world
}

package_postgresql93-libs() {
  pkgdesc="Libraries for use with PostgreSQL"
  depends=('krb5' 'openssl>=1.0.0' 'readline>=6.0')
  provides=('postgresql93-client')

  cd "${srcdir}/postgresql-${pkgver}"

  # install license
  install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgbase}-libs/LICENSE"

  # install libs
  for dir in src/interfaces src/bin/pg_config src/bin/psql src/bin/pg_dump; do
    make -C ${dir} DESTDIR="${pkgdir}" install
  done

  install -D -m644 doc/src/sgml/man1/pg_config.1 "${pkgdir}/usr/local/${_pkginsmap}/share/man/man1/pg_config.1"
  install -D -m644 doc/src/sgml/man1/pg_dump.1 "${pkgdir}/usr/local/${_pkginsmap}/share/man/man1/pg_dump.1"
  install -D -m644 doc/src/sgml/man1/pg_dumpall.1 "${pkgdir}/usr/local/${_pkginsmap}/share/man/man1/pg_dumpall.1"
  install -D -m644 doc/src/sgml/man1/pg_restore.1 "${pkgdir}/usr/local/${_pkginsmap}/share/man/man1/pg_restore.1"
  install -D -m644 doc/src/sgml/man1/psql.1 "${pkgdir}/usr/local/${_pkginsmap}/share/man/man1/psql.1"

  cd src/include

  mkdir -p "${pkgdir}"/usr/local/${_pkginsmap}/include/{libpq,postgresql/internal/libpq}

  # these headers are needed by the public headers of the interfaces
  install -m644 pg_config.h "${pkgdir}/usr/local/${_pkginsmap}/include/"
  install -m644 pg_config_os.h "${pkgdir}/usr/local/${_pkginsmap}/include/"
  install -m644 pg_config_ext.h "${pkgdir}/usr/local/${_pkginsmap}/include/"
  install -m644 postgres_ext.h "${pkgdir}/usr/local/${_pkginsmap}/include/"
  install -m644 libpq/libpq-fs.h "${pkgdir}/usr/local/${_pkginsmap}/include/libpq/"
  install -m644 pg_config_manual.h "${pkgdir}/usr/local/${_pkginsmap}/include/"

  # these headers are needed by the not-so-public headers of the interfaces
  install -m644 c.h "${pkgdir}/usr/local/${_pkginsmap}/include/postgresql/internal/"
  install -m644 port.h "${pkgdir}/usr/local/${_pkginsmap}/include/postgresql/internal/"
  install -m644 postgres_fe.h "${pkgdir}/usr/local/${_pkginsmap}/include/postgresql/internal/"
  install -m644 libpq/pqcomm.h "${pkgdir}/usr/local/${_pkginsmap}/include/postgresql/internal/libpq/"
}

package_postgresql93-docs() {
  pkgdesc="HTML documentation for PostgreSQL"
  options=('docs')

  cd "${srcdir}/postgresql-${pkgver}"

  # install license
  install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgbase}-docs/LICENSE"

  make -C doc/src/sgml DESTDIR="${pkgdir}" install-html
  chown -R root:root "${pkgdir}/usr/local/${_pkginsmap}/share/doc/postgresql/html/"

  # clean up
  rmdir "${pkgdir}"/usr/local/${_pkginsmap}/share/man/man{1,3,7}
  rmdir "${pkgdir}"/usr/local/${_pkginsmap}/share/man
}

package_postgresql93() {
  pkgdesc="A sophisticated object-relational DBMS"
  backup=('etc/pam.d/postgresql' 'etc/logrotate.d/postgresql')
  depends=("postgresql93-libs>=${pkgver}" 'krb5' 'libxml2' 'readline>=6.0' 'openssl>=1.0.0')
  optdepends=('python2: for PL/Python support'
              'perl: for PL/Perl support'
              'tcl: for PL/Tcl support')
  options=('staticlibs')
  install=postgresql93.install

  cd "${srcdir}/postgresql-${pkgver}"

  # install
  make DESTDIR="${pkgdir}" install
  make -C contrib DESTDIR="${pkgdir}" install
  make -C doc/src/sgml DESTDIR="${pkgdir}" install-man

  # we don't want these, they are in the -libs package
  for dir in src/interfaces src/bin/pg_config src/bin/psql src/bin/pg_dump; do
    make -C ${dir} DESTDIR="${pkgdir}" uninstall
  done
  rm "${pkgdir}/usr/local/${_pkginsmap}/share/man/man1/pg_config.1"
  rm "${pkgdir}/usr/local/${_pkginsmap}/share/man/man1/pg_dump.1"
  rm "${pkgdir}/usr/local/${_pkginsmap}/share/man/man1/pg_dumpall.1"
  rm "${pkgdir}/usr/local/${_pkginsmap}/share/man/man1/pg_restore.1"
  rm "${pkgdir}/usr/local/${_pkginsmap}/share/man/man1/psql.1"

  # install license
  install -D -m644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgbase}/LICENSE"

  # clean up unneeded installed items
  rm -rf "${pkgdir}/usr/local/${_pkginsmap}/include/postgresql/internal"
  rm -rf "${pkgdir}/usr/local/${_pkginsmap}/include/libpq"
  find "${pkgdir}/usr//local/${_pkginsmap}/include" -maxdepth 1 -type f -execdir rm {} +
  rmdir "${pkgdir}/usr/local/${_pkginsmap}/share/doc/postgresql/html"

  install -D -m644 "${srcdir}/postgresql93.tmpfiles.conf" \
    "${pkgdir}/usr/lib/tmpfiles.d/postgresql93.conf"
  install -D -m644 "${srcdir}/postgresql93.service" \
    "${pkgdir}/usr/lib/systemd/system/postgresql93.service"
  install -D -m755 "${srcdir}/postgresql-check-db-dir" \
    "${pkgdir}/usr/local/${_pkginsmap}/bin/postgresql-check-db-dir"

  install -D -m644 "${srcdir}/postgresql.pam" \
    "${pkgdir}/etc/pam.d/postgresql"

  install -D -m644 "${srcdir}/postgresql.logrotate" \
    "${pkgdir}/etc/logrotate.d/postgresql"
}
